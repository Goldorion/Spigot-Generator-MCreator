import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'java'
    id "de.undercouch.download" version "4.1.1"
}

group 'your.group'
version '1.0'

sourceCompatibility = '16'
targetCompatibility = '16'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
}

jar {
    archiveFileName = 'plugin-1.17.1.jar'
}

task buildLatestSpigot(type: Download)  {
	src 'https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar'
    dest 'build/btmp/BuildTools.jar'
	doLast {
		javaexec {
			main = "-jar"
			args = [ "BuildTools.jar", "--rev", "1.17.1" ]
			workingDir = "build/btmp"
		}
		copy {
			from file('build/btmp/spigot-1.17.1.jar')
			into file('run/')
		}
		delete files('build/btmp')
	}
}


task runSpigotServer (dependsOn: build) {
	doLast {
		copy {
			from file('build/libs/plugin-1.17.1.jar')
			into file('run/plugins/')
		}
		javaexec {
			main = "-jar"
			args = [ "spigot-1.17.1.jar" ]
			workingDir = "run/"
		}
	}
}
