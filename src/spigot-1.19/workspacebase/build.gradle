import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'java'
    id "de.undercouch.download" version "4.1.1"
}

group 'your.group'
version '1.0'

sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:' + project.minecraft + '-R0.1-SNAPSHOT'
}

jar {
    archiveFileName = 'plugin-' + project.minecraft + '.jar'
}

task buildLatestSpigot(type: Download)  {
	src 'https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar'
    dest 'build/btmp/BuildTools.jar'
	doLast {
		javaexec {
			main = "-jar"
			args = [ "BuildTools.jar", "--rev", project.minecraft ]
			workingDir = "build/btmp"
		}
		copy {
			from file('build/btmp/spigot-' + project.minecraft + '.jar')
			into file('run/')
		}
		delete files('build/btmp')
	}
}


task runSpigotServer (dependsOn: build) {
	doLast {
		copy {
			from file('build/libs/plugin-' + project.minecraft + '.jar')
			into file('run/plugins/')
		}
		javaexec {
			main = "-jar"
			args = [ "spigot-" + project.minecraft + ".jar" ]
			workingDir = "run/"
		}
	}
}
